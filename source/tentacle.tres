[gd_resource type="Shader" format=2]

[resource]
code = "shader_type canvas_item;

uniform sampler2D vertices_array; 
uniform int array_count;

uniform vec4 color_outline: hint_color;
uniform vec4 color_shadow: hint_color;
uniform vec4 color_tentacle: hint_color; 
uniform vec4 color_suction: hint_color; 

const float width_start = 30.0;
const float width_end = 10.0;


vec4 from_vertices(int r, int c)
{
	return texelFetch(vertices_array, ivec2(c, r), 0);
}

vec2 position_from_vertices(int r, int c)
{
	return from_vertices(r, c).xy * vec2(1280.0, 720.0);
}


int line_count(int r)
{
	return int(from_vertices(r, 0).x);
}

float lerp(float a, float b, float t)
{
	return a + t * (b - a);
}


vec2 point_on_line(vec2 a, vec2 b, vec2 p)
{
	vec2 ab = b - a; 
	vec2 ap = p - a;
	
	float ab_ap = dot(ap, ab);
	float ab_ab = dot(ab, ab); 
	
	float d = ab_ap / ab_ab;
	
	if (d <= 0.0) return a;
	if (d >= 1.0) return b;
	
	return a + d * ab;
}


float distance_to_line(vec2 a, vec2 b, vec2 p)
{
	vec2 pl = point_on_line(a, b, p);
	return length(pl - p);
}


float line_length(int r) 
{
	int count = line_count(r);
	
	float total_length = 0.0;
	
	for (int c = 2; c < count + 1; ++c)
	{
		vec2 a = position_from_vertices(r, c - 1);
		vec2 b = position_from_vertices(r, c);
		
		total_length += length(b - a);
	}
	
	return total_length;
}


vec4 line_color(int r, vec2 p)
{
	vec4 color = vec4(0.0);
	
	int count = line_count(r);
	
	float lt = line_length(r);
	float lc = 0.0;
	
	for (int c = 2; c < count + 1; ++c)
	{
		vec2 a = position_from_vertices(r, c - 1);
		vec2 b = position_from_vertices(r, c);

		vec2 pl = point_on_line(a, b, p);
		float dp = length(pl - p);
		float dpl = length(pl - a);
		
		float t = (lc + dpl) / lt;
		
		float w = lerp(width_start, width_end, t);
		
		if (dp < w - 10.0) 
			color = color_tentacle;
		if (dp < w - 5.0 && color != color_tentacle) 
			color = color_shadow;
		if (dp < w && color == vec4(0.0)) 
			color = color_outline;
		
		lc += length(b - a);
	}
	
	for (int c = 1; c < count + 1; c += 3)
	{
		vec2 a = position_from_vertices(r, c);
		float d = length(p - a);
		
		if (4.0 < d && d < 7.0)
		{
			color = color_suction;
		} 
	}
	
	return color;
}


void fragment() 
{
	vec2 p = UV * vec2(1280.0, 720.0);
	
	vec4 color = vec4(0.0);
	
	for (int r = 0; r < array_count; ++r)
	{
		vec4 c = line_color(r, p);
		if (c.a != 0.0)
		{
			color = c;
			break;
		}
	}
	
	COLOR = color;
}"
